import "utils/pack/pack128.code" as pack128
import "utils/pack/unpack128.code" as unpack128
import "hashes/sha256/1024bitPadded.code" as sha256

// A function that takes an array of 8 field elements as inputs, unpacks each of them to 128
// bits (big endian), concatenates them and applies sha256.
// It then returns an array of  two field elements, each representing 128 bits of the result.

def main(field[8] preimage) -> (field[2]):

    a = unpack128(preimage[0])
    b = unpack128(preimage[1])
    c = unpack128(preimage[2])
    d = unpack128(preimage[3])
    e = unpack128(preimage[4])
    f = unpack128(preimage[5])
    g = unpack128(preimage[6])
    h = unpack128(preimage[7])

    field[256] lhs1 = [...a, ...b]
    field[256] lhs2 = [...c, ...d]
    field[256] rhs1 = [...e, ...f]
    field[256] rhs2 = [...g, ...h]

    field[256] r = sha256(lhs1, lhs2, rhs1, rhs2)

    res0 = pack128(r[..128])
    res1 = pack128(r[128..])

    return [res0, res1]
